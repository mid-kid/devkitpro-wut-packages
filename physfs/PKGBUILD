# Maintainer: TurtleP <jpostelnek@outlook.com>

pkgname=wiiu-physfs
pkgver=3.0.2
pkgrel=2
pkgdesc='A library to provide abstract access to various archives.'
arch=('any')
url='https://icculus.org/physfs'
license=('zlib')
options=(!strip libtool staticlibs)
source=("https://icculus.org/physfs/downloads/physfs-${pkgver}.tar.bz2" "physfs-${pkgver}.patch")
makedepends=('devkitPPC' 'dkp-toolchain-vars' 'wiiu-cmake')
sha256sums=(
  '304df76206d633df5360e738b138c94e82ccf086e50ba84f456d3f8432f9f863'
  '15802af4f03a02dc8892aee595d9ebab8756b685f5dd1ccac38a3047a95587b4'
)
groups=('wiiu-portlibs')

build() {
  cd physfs-$pkgver

  patch -Np1 -i "$srcdir"/physfs-${pkgver}.patch

  "$DEVKITPRO"/portlibs/wiiu/bin/powerpc-eabi-cmake -G"Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX="$DEVKITPRO"/portlibs/wiiu \
    -DPHYSFS_BUILD_STATIC=ON -DPHYSFS_BUILD_SHARED=OFF \
    -DPHYSFS_BUILD_TEST=OFF \
    .

  make
}

package() {
  cd physfs-$pkgver
  make install DESTDIR="$pkgdir"
}
